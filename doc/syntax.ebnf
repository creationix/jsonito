root = value;

(*
  A single document can have zero or more inline shared values 
  or dictionary IDs followed by the root value.
*)
document = { value | dictionary }, value;

dictionary = varint, "@";



(* 
  Comments are considered whitespace 
  and can be inserted anywhere whitespace is allowed.
*)
comment = line_comment | block_comment ;
line_comment = "//", { ? UTF-8 Character Except Newline ? }, "\n";
block_comment = "/*", { ? UTF-8 Character ? }, "*/";


(*
  A value is a scalar, map, list, or scope.
  A scalar is a varint, string, integer, decimal, reference, true, false, or null.
  A map is a key/value pair.
  A list is a sequence of values.
  A scope is a set of definitions for references.
*)
value
  = varint, 
    ( string
    | number
    | reference
    | simple
    )
  | map | list
  ;

(* Zero or more key/value pairs, similar to JSON object *)
map = "{", { value, value }, "}";

(* Zero ot more values, similar to JSON array *)
list = "[", { value }, "]";

(* inline string such as `key.` *)
(* length prefixed string such as `5$Hello` *)
string = "'" | "~", { ? UTF-8 Character ? } ;


(* integers are encoded as zigzag(N) *)
(* power of 10 encoded using zigzag(exponent) *)
number = { ":", varint }, ".";

(* true = `!` false = `F!` null = `N!` *)
simple = "!";

(* Integer index into current scope *)
reference = "*";

(*
  An unsigned integer as zero or more digits.
  The leftmost is most significant in base 64.
  Encoding zero is an zero-length string.

  0 -> ""
  1 -> "1"
  10 -> "a"
  35 -> "z"
  36 -> "A"
  61 -> "Z"
  62 -> "-"
  63 -> "_"
  64 => "10"
*)
varint = { b64 };

(*
  This is the same digits as url-safe base64, but used as normal integer digits.
  Note that the order is slightly different to be an extension of typical hexadecimal.
*)
b64
  = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7"
  | "8" | "9" | "a" | "b" | "c" | "d" | "e" | "f"
  | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n"
  | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v"
  | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D"
  | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L"
  | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
  | "U" | "V" | "W" | "X" | "Y" | "Z" | "-" | "_"
  ;
